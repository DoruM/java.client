#
# Copyright 2007-2015, Kaazing Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

# Create Request
accept http://localhost:8001/echo/;e/cbm
accepted 
connected

read method "GET"
read version "HTTP/1.1"
read header "X-WebSocket-Version" "wseb-1.0"
read header "X-Sequence-No" /(?<sequence>[0-9]{1,3})/
read header "X-Accept-Commands" "ping"
read closed

write status "201" "Created" 
write version "HTTP/1.1"
write header "X-WebSocket-Version" "wseb-1.0"
write header "Content-Type" "text/plain;charset=utf-8"
write header content-length

write "http://localhost:8001/echo/;e/ubm/NMLBn6kH9nyesgt34tOvDai9EmdrGYSI\n"
write "http://localhost:8001/echo/;e/dbm/NMLBn6kH9nyesgt34tOvDai9EmdrGYSI\n"
write close

# Downstream
accept http://localhost:8001/echo/;e/dbm/NMLBn6kH9nyesgt34tOvDai9EmdrGYSI
accepted
connected

read method "POST"
read version "HTTP/1.1"
read header "X-Sequence-No" ${wse:asString(sequence + 1)}
read closed
read notify DOWNSTREAM_REQUEST_RECEIVED

write await LONG_POLLING_REQUEST_RECEIVED
write status "200" "OK"
write version "HTTP/1.1"
write header "Content-Type" "application/octet-stream"
write header "Connection" "close"

write [0x01 0x30 0x31 0xFF]
write close
write notify DOWNSTREAM_CLOSED 

# Overlapping Downstream (Longpolling)
accept http://localhost:8001/echo/;e/dbm/NMLBn6kH9nyesgt34tOvDai9EmdrGYSI?.ki=p
accepted
connected

read await DOWNSTREAM_REQUEST_RECEIVED
read method "POST"
read version "HTTP/1.1"
read header "X-Sequence-No" ${wse:asString(sequence + 2)}
read closed
read notify LONG_POLLING_REQUEST_RECEIVED

write await DOWNSTREAM_CLOSED
write status "200" "OK"
write version "HTTP/1.1"
write header "Content-Type" "application/octet-stream"
write header content-length

write [0x81 0x11]
write "Hello, WebSocket!"
write [0x01 0x30 0x32 0xFF]
write [0x01 0x30 0x31 0xFF]
write close

# Upstream
accept http://localhost:8001/echo/;e/ubm/NMLBn6kH9nyesgt34tOvDai9EmdrGYSI
accepted
connected

read method "POST"
read version "HTTP/1.1"
read header "X-Sequence-No" ${wse:asString(sequence + 1)}

read [0x01 0x30 0x32 0xFF]
read [0x01 0x30 0x31 0xFF]
read closed

write status "200" "OK"
write version "HTTP/1.1"
write header content-length
write close
